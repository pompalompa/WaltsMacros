'MacroName:MakeTitleAddedEntry.2016.01
'MacroDescription:Makes a title added entry field from selected text.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Last updated: 30 April 2015.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 2.63 and 64-bit Windows 7 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.
'
'****************************************************************************************
' How it works: Select some text in the record and run the macro. It asks whether to add
' the selected text as a 246 or a 740 field, and inserts the text with the chosen tag.
'
' If adding a 246 field, the macro makes several changes to the selected text. It
' capitalizes the first letter, if necessary, strips a final period, if present, and
' converts an ampersand to the word "and" if the language of the item is English. It sets
' the indicators to fill characters unless the ampersand is changed, in which case the
' indicators are set to "3 ". If the selection includes a properly formatted alternative
' title (that is, if the macro sees the string ", or, " as in "Title, or, Alternative
' title"), the macro will add a 246 field for each part of the title. In this case, an
' ampersand will remain unchanged, but an initial article ("a," "an," or "the") in the
' alternative title will be removed (again, if the language of the item is English).
'
' If adding a 740 field, the macro capitalizes the first letter, if necessary, adds a
' final period, if the field is lacking other terminal punctuation, and sets the first
' indicator for an initial article, if the language of the item is English. It sets the
' second indicator to "2", assuming that the title is a part of the item being cataloged.
'****************************************************************************************

Option Explicit

Declare Function InitialCapital( Target$ ) As String

'****************************************************************************************

Sub Main

Const CANCEL           As Integer = 102 'The value returned by the dialog box when the "Cancel" button is clicked.
Const CRITICAL_MESSAGE As Integer = 16  'The value to display a "Critical message" (which includes the red button with
                                        ' a big "X") in a message box; used to report the macro quitting.
Const DISPLAY_AT_TOP   As Integer = 1   'The value to display the fixed field at the top of a record.

Dim CS As Object
Set CS = CreateObject("Connex.Client")

Dim Lang$
Dim PosFF%
Dim Selection$
Dim Title1$
Dim Title2$
Dim WaltsMacros$         : WaltsMacros$ = "[Walt's macros] Extras1:MakeTitleAddedEntry"

Dim English

Dim p As Integer, q As Integer

' First, get the selected text.

If CS.GetSelectedText (Selection$) = FALSE Then
    MsgBox "To create a 246 or 740, first select some text.", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End If

' InitialCapital the first character.

Selection$ = Trim$( Selection$ )
Selection$ = InitialCapital$( Selection$ )

' Check the fixed field to see if the record is in English for removal of initial
' articles and conversion of the ampersand.

PosFF% = CS.FixedFieldPosition
If PosFF% <> DISPLAY_AT_TOP Then CS.FixedFieldPosition = DISPLAY_AT_TOP

If CS.GetFixedField ( "Lang", Lang$ ) Then
    If Lang$ = "eng" Or Lang$ = "zxx" Then English = TRUE
End If

CS.FixedFieldPosition = PosFF%

' The dialog box to determine whether to add the selected text as a 246 or a 740 field.

Begin Dialog Dialog1Definition 188, 92, WaltsMacros$
  Text             54, 10, 80,  8, "Insert selected text as ..."
  ButtonGroup .choice
    PushButton     30, 32, 52, 16, "&246"
    PushButton    106, 32, 52, 16, "&740"
    CancelButton   68, 60, 52, 16
End Dialog
Dim Dialog1 As Dialog1Definition
On Error Resume Next
Dialog Dialog1
If Err = CANCEL Then Exit Sub

Select Case Dialog1.choice
  Case 0                     ' Insert text as 246
' Excise a terminal full stop
    If Right$( Selection$, 1 ) = "." Then
        Selection$ = Left$( Selection$, Len( Selection$ ) - 1 )
        Selection$ = Trim$( Selection$ )
    End If
' Look for an alternative title, and if found, add two 246 fields
    p = InStr( Selection$, ", or, " )
    If p <> 0 Then
        Title1$ = Left$( Selection$, p - 1 )
        Title2$ = Mid$( Selection$, p + 6 )
        If English = TRUE Then
            If Left$( Title2$, 2 ) = "A " Then
                Title2$ = Mid$( Title2$, 3 )
              ElseIf Left$( Title2$, 3 ) = "An " Then
                Title2$ = Mid$( Title2$, 4 )
              ElseIf Left$( Title2$, 4 ) = "The " Then
                Title2$ = Mid$( Title2$, 5 )
            End If
            Title2$ = InitialCapital$( Title2$ )
        End If
        Title1$ = "24630" & Title1$
        Title2$ = "24630" & Title2$
        If CS.AddField (1, Title1$) = FALSE Or CS.AddField (2, Title2$) = FALSE Then
            MsgBox "Sorry, cannot create 246.", CRITICAL_MESSAGE, WaltsMacros$
            Exit Sub
        End If
        CS.CursorColumn = 4
        SendKeys "+{RIGHT}"
      Else
        q = InStr( Selection$, " & " )
        If q <> 0 And English = TRUE Then
            Selection$ = Left$( Selection$, q ) & "and" & Mid$( Selection$, q + 2 )
            Selection$ = "2463 " & Selection$
          Else
            Selection$ = "246" & Chr$( 252 ) & Chr$( 252 ) & Selection$
        End If
        If CS.AddField (1, Selection$) = FALSE Then
            MsgBox "Sorry, cannot create 246.", CRITICAL_MESSAGE, WaltsMacros$
            Exit Sub
        End If
        CS.CursorColumn = 4
        SendKeys "+{RIGHT}"
    End If
  Case 1
    If English = TRUE Then
        If Left$ ( Title2$, 2 ) = "A " Then
            Selection$ = "74022" & Selection$
          ElseIf Left$ ( Title2$, 3 ) = "An " Then
            Selection$ = "74032" & Selection$
          ElseIf Left$ ( Title2$, 4 ) = "The " Then
            Selection$ = "74042" & Selection$
          Else
            Selection$ = "74002" & Selection$
        End If
      Else
        Selection$ = "74002" & Selection$
    End If
    If Asc( Right$( Selection$, 1 ) ) > 46 Then Selection$ = Selection$ & "."
    If CS.AddField (1, Selection$) = FALSE Then MsgBox "Sorry, cannot create 740.", CRITICAL_MESSAGE, WaltsMacros$

End Select

End Sub

'****************************************************************************************

Function InitialCapital( Target$ ) As String

If Left$( Target$, 1 ) Like "[a-z]" Then
    Mid( Target$, 1, 1 ) = UCase$( Left$( Target$, 1 ) )
End If
InitialCapital = Target$

End Function
'2038868
'
'Macro name: MakeTitleAddedEntry
'Macro book: C:\Program Files (x86)\OCLC\Connexion\Program\Macros\Extras1.mbk
'Saved: 12/17/2018 9:06:05 AM using "MacroBookInspector" macro by Walter F. Nickeson.
