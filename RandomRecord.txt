'MacroName:RandomRecord.2017.01
'MacroDescription:Searches for a random bibliographic record.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Last updated: 10 August 2015.
' Check for the latest versions of this and my other macros at
' http://docushare.lib.rochester.edu/docushare/dsweb/View/Collection-2556
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 2.63 and 64-bit Windows 7 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.
'
'****************************************************************************************
' How it works: Run this macro to search WorldCat for a random bibliographic record. The
' macro generates a random number of 9 digits (to be 10 digits in the near future) for
' the search. If no record is found after 12 attempts, the macro stops, as that could be
' a sign of connection issues. Otherwise, the macro quits when a record is found.
'
' The current highest OCLC control number can be found at
' http://www.oclc.org/worldcat/watch-worldcat-grow.en.html.
'****************************************************************************************

Option Explicit

'****************************************************************************************

Sub Main

Const CRITICAL_MESSAGE  As Integer = 16  'The value to display a "Critical message" (which includes the red button with
                                         ' a big "X") in a message box; used to report the macro quitting.
Const MAX_LENGTH_OCLCCN As Integer = 9   'The maximum number of digits in an OCLC control number. This will be increased
                                         ' from 9 to 10 some time during summer 2017.

Dim CS As Object
Set CS = CreateObject("Connex.Client")

Dim ControlNumber$
Dim RandomNumber&
Dim Results&
Dim SearchCount%
Dim Seed                     : Seed         = Timer
Dim WaltsMacros$             : WaltsMacros$ = "[Walt's macros] MacroTools:RandomRecord"

' Of course, you must be online before trying to retrieve a record!

If CS.IsOnline = FALSE Then
    MsgBox "Please log on before trying to find a record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End If

Do

' Get a random number of digits no larger than the maximum number of the current largest
' OCLC control number. Initial zeroes will be ignored in searching, so not every search
' string will be of that length.

  Randomize Seed
  RandomNumber&  = Fix( Rnd * 10 ^ MAX_LENGTH_OCLCCN )
  ControlNumber$ = "#" & Trim$( Str$( RandomNumber& ) )
  Results&       = CS.Search( "WC", ControlNumber$ )

' If a record is retrieved, the macro is done. Otherwise, use the failed search number as
' the seed for a new random number for the next search. However, if there is a connection
' problem such that all searches are failing, provide an exit from a possible endless
' loop.

  If Results& > 0 Then
      Exit Do
    Else
      Seed         = Val( Mid$( ControlNumber$, 2 ) )
      SearchCount% = SearchCount% + 1
      If SearchCount% = 13 Then
          MsgBox "No record was found in 12 tries. Please check the connection before trying again.", CRITICAL_MESSAGE, WaltsMacros$
          Exit Sub
      End If
  End If

Loop Until Results& > 0

End Sub
'88
'
'Macro name: RandomRecord
'Macro book: C:\Program Files (x86)\OCLC\Connexion\Program\Macros\MacroTools.mbk
'Saved: 6/28/2017 11:37:43 AM using "MacroBookInspector" macro by Walter F. Nickeson.
