'MacroName:RandomRecord.2017.08
'MacroDescription:Searches for a random bibliographic record.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Last updated: 10 August 2017.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 2.63 and 64-bit Windows 7 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.
'
'****************************************************************************************
' How it works: Run this macro to search WorldCat for a random bibliographic record. The
' macro generates a random number of 10 digits for the search, repeating until a record
' is found or 12 searches fail.
'
' The current highest OCLC control number can be found at
' http://www.oclc.org/en/worldcat/watch-worldcat-grow-popup.html
'****************************************************************************************

Option Explicit

'****************************************************************************************

Sub Main

Const CRITICAL_MESSAGE As Integer = 16     'The value to display a "Critical message" (which includes the red button with a
                                           ' big "X") in a message box; used to report the macro quitting.
Const MAX_ATTEMPTS     As Integer = 13     'The maximum number of search attempts per try.
Const ONE_BILLION      As Single  = 10^10  'What to multiply a number between 0 and 1 by to get a 10-digit number with which to
                                           ' search WorldCat.
Const OVERFLOW         As Integer = 6      'The BASIC error code for an overflow.

Dim CS As Object
Set CS = CreateObject("Connex.Client")

Dim ControlNumber$
Dim LongRandomNumber&
Dim NewRandomNumber!
Dim Results&
Dim SearchCount%
Dim Seed                     : Seed         = Timer
Dim WaltsMacros$             : WaltsMacros$ = "[Walt's macros] MacroTools:RandomRecord"

' Of course, you must be online before trying to retrieve a record!

If CS.IsOnline = FALSE Then
    MsgBox "Please log on before trying to find a record!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End If

' The random number generator produces a number between 0 and 1. Multiply this by a
' billion and drop decimals to get a random number consisting of ten digits. (Leading
' zeros are ignored in searches.) Convert that number to a string, and search WorldCat.
' If no record is retrieved, generate a different number and try again--but limit the
' number of attempts to 12.

Do
  Randomize Seed
  NewRandomNumber!  = Rnd
  On Error Resume Next
  LongRandomNumber& = Fix( NewRandomNumber! * ONE_BILLION )

' The largest number that can be of the data type "long" is 2,147,483,647. If the
' generated random number is larger than that, an overflow error occurs. In that case,
' generate a new number. By the time the OCLC control number reaches 2 billion, macros
' probably won't even exist, so there won't be a need to go to the next wider data type
' ("single").

  If Err = OVERFLOW Then
      Seed = Rnd( 0 )
      GoTo KeepGoing:
    ElseIf Err > 0 Then
      MsgBox "Sorry, the macro unexpectedly failed.", CRITICAL_MESSAGE, WaltsMacros$
      Exit Sub
  End If
  ControlNumber$ = "#" & Trim$( Str$( LongRandomNumber& ) )
  Results&       = CS.Search( "WC", ControlNumber$ )

' If a record is retrieved, the macro is done. Otherwise, use the failed search number as
' the seed for a new random number for the next search. However, if there is a connection
' problem such that all searches are failing, or if the generated numbers continually
' fail to find a record, provide an exit from a possible endless loop.

  If Results& > 0 Then
      Exit Do
    Else
      Seed         = Val( Mid$( ControlNumber$, 2 ) )
      SearchCount% = SearchCount% + 1
      If SearchCount% = MAX_ATTEMPTS Then
          MsgBox "No record was found in " & Trim$( Str$( MAX_ATTEMPTS) ) & " attempts. Please check the connection before trying again.", CRITICAL_MESSAGE, WaltsMacros$
          Exit Sub
      End If
  End If

KeepGoing:

Loop Until Results& > 0

End Sub
'1316240
'
'Macro name: RandomRecord
'Macro book: C:\Program Files (x86)\OCLC\Connexion\Program\Macros\MacroTools.mbk
'Saved: 12/17/2018 9:15:10 AM using "MacroBookInspector" macro by Walter F. Nickeson.
