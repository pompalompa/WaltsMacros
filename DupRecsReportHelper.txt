'MacroName:DupRecsReportHelper.2018.01
'MacroDescription:Gets the OCLC control numbers of all opened bibliographic records and
' writes them to a text file to help in reporting duplicate records to OCLC.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Last updated: 19 January 2018.
'
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 2.63 and 64-bit Windows 7 Enterprise &
' 64-bit Windows 10 Pro, but no guarantees are promised or implied.
'
'***************************************************************************************#
' How it works: Select a group of records whose numbers you want to report to OCLC as
' duplicates by having them all open in their own windows (thus all, or all but one, are
' pinned). Identify the preferred record by putting an exclamation mark ("!") somewhere
' in the 040. Run the macro. It cycles through all the open windows, harvests the OCLC
' control numbers of all the bibliographic records, and writes them to a text file.
' Finally, the name and path of the file are placed on the Windows Clipboard so it can be
' pasted into the Windows "Run" command for quick opening of the file by the default text
' editor. The first line of this file is the URI of the "WorldCat Duplicate Record Merge
' Request" on OCLC's web site; if your text editor makes URIs clickable, you can thus
' open your browser directly from the file. When the Merge Request web form is open you
' can (relatively easily) copy and paste the control numbers into its fields by toggling
' back and forth between the browser and the text editor. Records with LC copy, as shown
' by the 040, are indicated by "DLC" appearing after the record number. The preferred
' record number, if the 040 was marked with the exclamation mark, appears first on a line
' by itself.
'
' To change the name and path of the file, change the value of the variable
' "DupFileName$" on line 62 below.
'****************************************************************************************

Option Explicit

Declare Function CompileNumbers( PrevString$ )

Global CRLF$
Global OCLCNumber$
Global Preferred As Variant
Global PreferredLine$
Global RecordCount%

Global CS As Object

'****************************************************************************************

Sub Main

Const CRITICAL_MESSAGE    As Integer = 16  'The value to display a "Critical message" (which includes the red button with
                                           ' a big "X") in a message box; used to report the macro quitting.
Const INFORMATION_MESSAGE As Integer = 64  'The value to display the "Information message" icon in a message box.

Set CS = CreateObject("Connex.Client")

'====  CHANGE THIS FILE PATH IF DESIRED  ================================================
Dim DupFileName$             : DupFileName$ = "c:\DuplicateOCLCRecs.txt"
'========================================================================================

Dim BibCount%
Dim CountOfWindows%
Dim DupReportForm$
Dim FileNumber%
Dim MessageA$
Dim MessageB$
Dim TypeOfWindow%
Dim WaltsMacros$             : WaltsMacros$ = "[Walt's macros] Extras2:DupRecsReportHelper"

Dim i As Integer

CRLF$          = Chr$( 013 ) & Chr$( 010 )     'Standard Windows new line characters
DupReportForm$ = "http://www.oclc.org/content/forms/worldwide/en/record-quality.html" & CRLF$ & CRLF$

Preferred = FALSE

' First make sure that a bibliographic record is open, to start with, at least (other
' kinds of windows, such as lists or authority records, may be open also).

TypeOfWindow% = CS.ItemType
Select Case TypeOfWindow%
  Case -1, 2 To 16, 18 To 25
    MsgBox "Please have all the bibliographic records open in their own windows.", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
End Select

' Compile the list by cycling through all the open windows and getting the control number
' from each bibliographic record.

CountOfWindows% = CS.WindowCount
i = 0
Do Until i = CountOfWindows%
  CS.SetTopWindow( i )
  TypeOfWindow% = CS.ItemType
  Select Case TypeOfWindow%
    Case 0, 1, 17
      Clipboard.Clear
      CS.CopyControlNumber
      OCLCNumber$ = CompileNumbers( OCLCNumber$ )
      BibCount%   = BibCount% + 1
  End Select
  i = i + 1
Loop

' Report on what the macro did.

If RecordCount% = 1 Then
    MsgBox "Only one bibliographic record was displayed!", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
  ElseIf OCLCNumber$ = "" Then
    MsgBox "Sorry, the macro failed.", CRITICAL_MESSAGE, WaltsMacros$
    Exit Sub
  Else
    MessageA$ = CStr( RecordCount% ) & " record numbers were written to " & DupFileName$ & "."
End If
MessageB$ = "The file name and path were put on the Clipboard to paste into the " & Chr$( 034 ) & "Run" & Chr$( 034 ) & " command."
MsgBox MessageA$ & CRLF$ & CRLF$ & MessageB$, INFORMATION_MESSAGE, WaltsMacros$

' Write the control numbers to the file.

If Preferred = TRUE Then OCLCNumber$ = PreferredLine$ & OCLCNumber$

FileNumber% = FreeFile
Open DupFileName$ For Output As #FileNumber%
Print #FileNumber%, DupReportForm$ ; OCLCNumber$
Close #FileNumber%
Reset

Clipboard.Clear
Clipboard.SetText DupFileName$

End Sub

'****************************************************************************************

Function CompileNumbers( PrevString$ )

' This function receives the string containing the OCLC control numbers of all the
' records examined thus far and adds the control number of the current record at the end.
' If it finds "DLC" in the current record's 040, that is added after the control number.
' If it finds an exclamation mark in the 040, indicating the preferred record, that
' control number is put on its own line and marked with angle brackets. Only the first
' record the macro encounters with an exclamation mark can be considered preferred.

Dim FieldData$
Dim OCLCControl$

Dim p As Integer

OCLCControl$ = Clipboard.GetText()

If CS.GetField( "040", 1, FieldData$ ) = TRUE Then
    p = InStr( FieldData$, "DLC" )
    If p <> 0 Then OCLCControl$ = OCLCControl$ & " DLC"
End If

If InStr( FieldData$, "!" ) <> 0 Then
    If Preferred = TRUE Then         'A record has already been marked as preferred.
        GoTo Skip:
      Else
        Preferred      = TRUE
        PreferredLine$ = "Preferred record: " & OCLCControl$ & CRLF$ & CRLF$
    End If
  Else
Skip:
    If RecordCount% = 0 Or PrevString$ = "" Then
        PrevString$ = OCLCControl$
      Else
        PrevString$ = PrevString$ & CRLF$ & OCLCControl$
    End If
End If
RecordCount%   = RecordCount% + 1
CompileNumbers = PrevString$

End Function
'2068814
'
'Macro name: DupRecsReportHelper
'Macro book: C:\Program Files (x86)\OCLC\Connexion\Program\Macros\Extras2.mbk
'Saved: 12/17/2018 9:12:33 AM using "MacroBookInspector" macro by Walter F. Nickeson.
