'MacroName:FormatCopied6XX
'MacroDescription:Formats subject fields copied from outside Connexion, cnanging dashes
' to delimiters and adding some subfield codes.
'
' This macro was written by Walter F. Nickeson,
' University of Rochester, Rochester, NY
' wnickeson@library.rochester.edu
'
' Last updated: 19 April 2015.
' Check for the latest versions of this and my other macros at
' https://github.com/wnickeson/WaltsMacros
' Please e-mail me with bug reports or to suggest improvements.
'
' This macro works for me with Connexion client 2.51 and Windows 7 Pro & Windows 10 Pro,
' but no guarantees are promised or implied.
'
'****************************************************************************************
' How it works: This macro formats subject headings pasted into Connexion from a non-MARC
' source, such as from the Web. It changes dashes to delimiters, with a standard subfield
' code of "x" or "y", and it changes the pipe symbol ("|") and dollar sign ("$") to the
' standard OCLC delimiter.
'
' Place the cursor in the field, which must already be tagged as a subject, and run the
' macro. For example, it changes this field, copied directly from LC's online catalog
' (with tagging supplied manually, and note that the delimiter doesn't display in this
' context, so the dollar sign is used):
'
'   650   0 Poets, Irish--20th century--Biography.
'
' to this:
'
'   650   0 Poets, Irish $y 20th century $x Biography.
'
' If dashes are replaced, the macro displays a warning message to check subfield coding,
' as of course it cannot tell whether any particular subfield should be "v," "x," "y," or
' "z" (although if it finds that the first character in a subfield is a digit, the macro
' will assume the subfield is a chronological subdivision and will code it "y").
'
' Replacing the pipe symbol is simple and straightforward.
'****************************************************************************************

Option Explicit

'****************************************************************************************

Sub Main

Const CRITICAL_MESSAGE    As Integer = 16  'The value to display a "Critical message" (which includes the red button with
                                           ' a big "X") in a message box; used to report the macro quitting.
Const INFORMATION_MESSAGE As Integer = 64  'The value to display the "Information message" icon in a message box.

Dim CS As Object
Set CS = CreateObject("Connex.Client")

Dim FieldData$
Dim NextChar%
Dim Row%
Dim Start%
Dim SubfieldCode$
Dim SubjectString$
Dim Tag$
Dim WaltsMacros$             : WaltsMacros$ = "[Walt's macros] Extras2:FormatCopied6XX"

Dim Changed                  : Changed      = FALSE
Dim Dashes                   : Dashes       = FALSE

Dim p As Integer

' First, get the data in the row in which the cursor is placed, and check that the field
' is tagged as a subject; this serves as a reminder to examine the tagging.

Row% = CS.CursorRow
If CS.GetFieldLine( Row%, FieldData$ ) = TRUE Then
    Tag$ = Left$( FieldData$, 5 )
    If Left$( FieldData$, 1 ) = "6" Then
        SubjectString$ = Mid$( FieldData$, 6 )
      Else
        MsgBox "This macro only works in fields tagged as subjects (6XX).", CRITICAL_MESSAGE, WaltsMacros$
        GoTo Done:
    End If
End If

' Then check for presence of dashes. First, see if there are dashes surrounded by spaces,
' and if there are, remove them.

Do
  p = InStr( SubjectString$, " -- " )
  If p <> 0 Then
      SubjectString$ = Left$( SubjectString$, p - 1 ) & "--" & Mid$( SubjectString$, p + 4 )
    Else
      Exit Do
  End If
Loop Until p = 0

' Now replace dashes with delimiters.

Do
  p = InStr( SubjectString$, "--" )
  If p <> 0 Then

' If a dash is found, check to see if the first character following is a digit; if it is,
' assume the subfield is a chronological subdivision, and make the subfield code "y";
' otherwise, make it "x".

      NextChar% = Asc( Mid$( SubjectString$, p + 2, 1 ) )
      If NextChar% > 47 And NextChar% < 58 Then
          SubfieldCode$ = "y "
        Else
          SubfieldCode$ = "x "
      End If

      SubjectString$ = Left$( SubjectString$, p - 1 ) & " " & Chr$( 223 ) & SubfieldCode$ & Mid$( SubjectString$, p + 2 )
      Changed = TRUE
      Dashes = TRUE
    Else
      If Changed = TRUE Then
          Exit Do
        Else
          GoTo Pipe:
      End If
  End If
Loop Until p = 0

Pipe:

' Change pipes to delimiters.

Do
  p = InStr( SubjectString$, Chr$( 124 ) )
  If p <> 0 Then
      SubjectString$ = Left$( SubjectString$, p - 1 ) & Chr$( 223 ) & Mid$( SubjectString$, p + 1)
      Changed = TRUE
    Else
      GoTo Dollar:
  End If
Loop until p = 0

Dollar:

' Change dollar signs to delimiters.

Do
  p = InStr( SubjectString$, Chr$( 36 ) )
  If p <> 0 Then
      SubjectString$ = Left$( SubjectString$, p - 1 ) & Chr$( 223 ) & Mid$( SubjectString$, p + 1)
      Changed = TRUE
    Else
      If Changed = FALSE Then
          MsgBox "Nothing was changed!", INFORMATION_MESSAGE, WaltsMacros$
          Exit Do
      End If
  End If
Loop until p = 0

' Now add the altered field.

SubjectString$ = Tag$ & SubjectString$
If CS.AddFieldLine( Row%, SubjectString$ ) = FALSE Then
    MsgBox "Sorry, could not format subject field.", CRITICAL_MESSAGE, WaltsMacros$
  Else
    If Dashes = TRUE Then MsgBox "Check subfield coding!", INFORMATION_MESSAGE, WaltsMacros$
End If

Done:

End Sub
'1937447
'
'Macro name: FormatCopied6XX
'Macro book: C:\Program Files (x86)\OCLC\Connexion\Program\Macros\Extras2.mbk
'Saved: 6/28/2017 11:36:05 AM using "MacroBookInspector" macro by Walter F. Nickeson.
